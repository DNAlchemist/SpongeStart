plugins {
  id "com.gradle.plugin-publish" version "0.9.4"
}

group 'com.qixalite'
version '1.4.2'

apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

dependencies {
    testCompile 'junit:junit:4.11'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'commons-io:commons-io:2.4'
    compile gradleApi()
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('.repo'))
        }
        if (project.hasProperty('qixaliteRepoUsername')){
            mavenDeployer {
                repository(url: 'http://repo.qixalite.com/content/repositories/public_releases/') {
                    authentication(userName: qixaliteRepoUsername, password: qixaliteRepoPassword)
                }
                snapshotRepository(url: 'http://repo.qixalite.com/content/repositories/public_snapshots') {
                    authentication(userName: qixaliteRepoUsername, password: qixaliteRepoPassword)
                }
            }
        }
    }
}

task setupPluginUpload << {

    def key=System.env.gradlePublishKey
    def secret = System.env.gradlePublishSecret

    if( !key || !secret)
    {
        throw new RuntimeException("gradlePublishKey and/or gradlePublishSecret are not defined environment variables")
    }

    System.properties.setProperty("gradle.publish.key", key)
    System.properties.setProperty("gradle.publish.secret", secret)
}

tasks.publishPlugins.dependsOn tasks.setupPluginUpload

pluginBundle {
  website = 'http://www.gradle.org/'
  vcsUrl = 'https://github.com/qixalite/spongestart/'
  description = "Gradle plugin to run sponge inside your workspace."
  tags = ["sponge", "minecraft"]

  plugins {
    spongestart {
      id = 'com.qixalite.spongestart'
      displayName = 'SpongeStart'
    }
  }
}
